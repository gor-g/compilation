/* package et imports */
package fr.usmb.m1isc.compilation.simple;
import java_cup.runtime.Symbol;
import java.util.HashMap;

/* inclusion du code */

action code {: 
    // pour utilisation dans les actions (classe action)
    // HashMap pour la gestion des variables
    private HashMap<String,Integer> vars = new HashMap<>();
    // gestion des erreurs d'evaluation
    private boolean erreur = false;
:};


parser code {:
    // pour le parser (redefinition de la methode reportant les erreurs d'analyse)
    public void report_error(String message, Object info) {
        String m = "";
        if (info instanceof java_cup.runtime.Symbol) {
            Symbol s = ((Symbol) info);     
            if (s != null && s.left >= 0 ) {
                /* Ajoute le numero de ligne  et de colonne*/
                m =  "Ligne " + (s.left+1) + ", Colonne " + (s.right+1) + " : ";
            }
        }
        m = m + message;
        System.err.println(m);
    }
:};

init with {:
    //    initialisation du parser
:};

/* symboles terminaux */
terminal PLUS, MOINS, MOINS_UNAIRE, MUL, DIV, MOD, NOT, OR, AND, PAR_G, PAR_D, SEMI, POINT, LET, INPUT, OUTPUT, IF, THEN, ELSE, WHILE, DO, EGAL, GT, GTE, LT, LTE, NIL, ERROR;
terminal Integer ENTIER;
terminal String IDENT;
/* symboles non terminaux */
non terminal SyntaxTree program, sequence, expression, expr;

precedence nonassoc OUTPUT;
precedence right OR;
precedence right AND;
precedence right NOT;
precedence nonassoc EGAL;
precedence nonassoc GT, GTE, LT, LTE;
precedence left PLUS, MOINS;
precedence left MUL, DIV, MOD;
precedence left MOINS_UNAIRE;

/* grammaire */
// un pg est une sequence d'insctions terminee par point
program ::= sequence:s {: RESULT = s; :} POINT;
// une sequence est une suite d'instructions separes par des point virgules
sequence ::=
        expression:e1 SEMI sequence:e2 {: RESULT = new SyntaxTree(Sym.SEMI, ";", e1, e2); :}
        | expression:e {: RESULT = e; :};
// une expession est soit une affectation ,une
expression ::=
       expr:e {: RESULT = e; :}
       | LET IDENT:nom EGAL expr:e {: RESULT = new SyntaxTree(Sym.LET, "let", new SyntaxTree(Sym.IDENT, nom.toString()), e); :}
       | WHILE expr:cond DO expression:e {: RESULT = new SyntaxTree(Sym.WHILE, "while", cond, e); :}
       | IF expr:cond THEN expression:a1 ELSE expression:a2 {: RESULT = new SyntaxTree(Sym.IF, "if", cond, new SyntaxTree(Sym.THEN, "then", a1, a2)); :}
       | error; // reprise d'erreurs
// expression arithmetiques et logiques
expr ::=
        NOT:op expr:e           {: RESULT = new SyntaxTree(Sym.NOT, "!", e, null); :}
       | expr:e1 AND expr:e2    {: RESULT = new SyntaxTree(Sym.AND, "&&", e1, e2); :}
       | expr:e1 OR expr:e2     {: RESULT = new SyntaxTree(Sym.OR, "||", e1, e2); :}
       | expr:e1 EGAL expr:e2   {: RESULT = new SyntaxTree(Sym.EGAL, "==", e1, e2); :}
       | expr:e1 GT expr:e2     {: RESULT = new SyntaxTree(Sym.GT, ">", e1, e2); :}
       | expr:e1 GTE expr:e2    {: RESULT = new SyntaxTree(Sym.GTE, ">=", e1, e2); :}
       | expr:e1 LT expr:e2     {: RESULT = new SyntaxTree(Sym.LT, "<", e1, e2); :}
       | expr:e1 LTE expr:e2    {: RESULT = new SyntaxTree(Sym.LTE, "<=", e1, e2); :}
       | expr:e1 PLUS expr:e2   {: RESULT = new SyntaxTree(Sym.ARITHMETIC, "+", e1, e2); :}
       | expr:e1 MOINS expr:e2  {: RESULT = new SyntaxTree(Sym.ARITHMETIC, "-", e1, e2); :}
       | expr:e1 MUL expr:e2    {: RESULT = new SyntaxTree(Sym.ARITHMETIC, "*", e1, e2); :}
       | expr:e1 DIV expr:e2    {: if (erreur) { RESULT = new SyntaxTree(); }
                                   else if (e2.getSymbolString().equals("0")) { RESULT = new SyntaxTree(); erreur = true; System.err.println("Ligne " + (e2left + 1) + ", Colonne " + (e2right + 1) + " : Erreur division par zero"); }
                                   else { RESULT = new SyntaxTree(Sym.ARITHMETIC, "/", e1, e2); }
                                :}
       | expr:e1 MOD expr:e2    {: if (erreur) { RESULT = new SyntaxTree(); }
                                   else if (e2.getSymbolString().equals("0")) { RESULT = new SyntaxTree(); erreur = true; System.err.println("Ligne " + (e2left + 1) + ", Colonne " + (e2right + 1) + " : Erreur division par zero"); }
                                   else { RESULT = new SyntaxTree(Sym.MOD, "%", e1, e2); }
                                :}
       | MOINS expr:e           {: RESULT = new SyntaxTree(Sym.MOINS_UNAIRE, "-", e, null); :} %prec MOINS_UNAIRE
       | OUTPUT expr:e          {: RESULT = new SyntaxTree(Sym.OUTPUT, "output", e, null); :}
       | INPUT                  {: RESULT = new SyntaxTree(Sym.INPUT, "input"); :}
       | NIL                    {: RESULT = new SyntaxTree(Sym.NIL, "nil"); :}
       | ENTIER:n               {: RESULT = new SyntaxTree(Sym.ENTIER, n.toString()); :}
       | IDENT:id               {: RESULT = new SyntaxTree(Sym.IDENT, id.toString()); :}
       | PAR_G sequence:e PAR_D {: RESULT = e; :};